# 
# Snakefile
# 
# run "pip3 install snakemake" with python3 installed
# 
# then execute: 
# snakemake
# 
# All of these rules use `shell:` since they are relatively simple, 
# but it is possible to use `run:` instead and execute arbitrary 
# python code within the rule. It is also possible to execute 
# arbitrary python outside of declared rules, i.e. the MODELS variable.

from os.path import dirname

MODELS = ['rf', 'lm']
INPUT_FILES = ['input/Train.csv', 'input/Machine_Appendix.csv']

_model_performance_csv = expand('working/models/{model}/test_predictions.csv', model=MODELS)

rule all: 
    input: 
        'working/models/model_performance.png', 
        actual_vs_predicted=expand('working/models/{model}/predicted_vs_actual.png', model=MODELS)

rule residuals_app: # doesn't work? 
    input: 
        csv=_model_performance_csv,
        script='launch_app.R'
    output: temp('.phony') # no phony rules yet, but this works
    shell: 'Rscript {input.script} {input.csv}'


rule predicted_vs_actual:
    input: 
        script='scripts/plot_predicted_vs_actual.R',
        test_predictions='working/models/{model}/test_predictions.csv'
    output: 'working/models/{model}/predicted_vs_actual.png'
    shell:
        'Rscript {input.script} {input.test_predictions} {output}'

rule model_performance:
    input: 
        csv=_model_performance_csv,
        script='scripts/model_performance.R'
    output: 'working/models/model_performance.png'
    shell: 'Rscript {input.script} "{input.csv}" {output}'


rule test_predictions:
    input:
        script='scripts/model.R',
        train='working/split/train.csv',
        test='working/split/test.csv'
    output: 'working/models/{model}/test_predictions.csv'
    shell: 
        'Rscript {input.script} {input.train} {input.test} {wildcards.model} {output}'


# Snakemake deals with multiple file targets, so we don't need to use a hack =)
rule split:
    input:
        csv='working/cleaned_input.csv',
        script='scripts/train_test_split.R'
    output: 
        train='working/split/train.csv', 
        test='working/split/test.csv'
    run: 
        outdir = dirname(output.train)
        shell('Rscript {input.script} {outdir} {input.csv}')

rule clean_input:
    input: 
        csv=INPUT_FILES,
        script='scripts/clean.R'
    output: 'working/cleaned_input.csv'
    shell: 'Rscript {input.script} {output} {input.csv}'

